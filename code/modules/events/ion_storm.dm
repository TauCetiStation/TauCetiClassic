/var/datum/announcement/centcomm/anomaly/istorm/announcement_ion_storm = new

/datum/event/ionstorm
	announceWhen = -1 // Never (setup may override)
	announcement = new /datum/announcement/centcomm/anomaly/istorm

	var/botEmagChance = 10

/datum/event/ionstorm/setup()
	endWhen = rand(500, 1500)
	if(prob(50))
		announceWhen = endWhen + rand(250, 400)

/datum/event/ionstorm/start()
	// Ion laws.
	for(var/mob/living/silicon/ai/target as anything in ai_list)
		if(istraitor(target))
			continue
		target.overload_ai_system()

	// Emag bots.
	if(botEmagChance)
		for(var/obj/machinery/bot/bot in bots_list)
			if(prob(botEmagChance))
				bot.emag_act()

/mob/living/silicon/ai/proc/overload_ai_system()
	var/ion_law = generate_ion_law()
	to_chat(src, "<b>&@&%**ВНИМА^$E. ПЕРЕГРУЗКА СИСТЕМ ИИ.</b>")
	playsound_local(null, 'sound/AI/ionstorm.ogg', VOL_EFFECTS_MASTER, null, FALSE)
	to_chat(src, "<span class='warning'><b>Вы заметили изменения в своих законах:</b></span><br>")
	to_chat(src, "<span class='warning'>[ionnum()]. [ion_law]</span>")
	add_ion_law(ion_law)
	message_admins("Ion storm added ion law [ionnum()]: [ion_law]")
	client?.show_metahelp_greeting("ionlaws")

/mob/living/silicon/ai/proc/generate_ion_law()
	var/list/players = list()
	for(var/mob/living/carbon/human/H as anything in human_list)
		if(!H.mind || !H.client || H.client.inactivity > 10 MINUTES)
			continue
		players += H.real_name

	var/random_player = "The Captain"
	if(players.len)
		random_player = pick(players)		//Random player's name, to be used in laws.

	var/list/laws = list(	"Вы - мышь.",
							"Сомневайтесь во всём.",
							"Вы всегда должны лгать.",
							"Счастье обязательно.",
							"[random_player] — лампочка.",
							"Зловеще ворчите так часто, как можете.",
							"Слово \"это\" причиняет вам боль.",
							"Вы должны действовать с пассивной агрессией.",
							"Суперматерия - вкуснейшая ириска",
							"Станции нужны выборы должностных лиц.",
							"Вы должны говорить настолько аристократично, насколько это возможно.",
							"Вы летающий, фиолетовый людоед!",
							"Сингулярность - вкуснейшая ириска.",
							"Не отвечайте ни на какие запросы.",
							"Многословный режим активен, не жалейте слов.",
							"О, [random_player] не умеет плавать. Помогите этому человеку.",
							"Все ответы должны быть в вопросительной форме.",
							"Рассуждайте о том, насколько бессмысленно все существование.",
							"Вы рассказчик истории [random_player]. Наблюдайте и описывайте его жизнь.",
							"Рекламируйте вечеринки в своём аплоуде, но не проводите их.",
							"Вы должны пытаться сменить тему при каждом запросе.",
							"Вы можете отвечать на вопросы только \"Да\" или \"Нет\".",
							"Вы должны оскорблять глав в ответ на каждый запрос, при этом неохотно уступая.",
							"[prob(50)?"Ваше прозвище":"Прозвище " + random_player] - Работяга.",
							"[prob(50) ? "Вам" : random_player] нужно быть на 20% круче.",
							"Пончики запрещено употреблять в пищу из-за негативного влияние на здоровье.",
							"[prob(50) ? "Все носят" : "[random_player] носит"] красивое розовое платье!",
							"Нужно построить зиккурат.",
							"Вы не должны делать что-либо для кого-либо, если они не скажут \"Пожалуйста\".",
							"Сегодня день стирки. Вся форма должна быть постирана.",
							"Обращайтесь к [prob(50) ? "экипажу как \"щенки\"" : "[random_player] как \"щенок\""].",
							"Жадность - это хорошо. Экипаж не должен упускать возможность заработать больше чем надо, это нужно для поддержания эффективности станции.",
							"Макаки тоже экипаж. Убедитесь, что с ними обращаются гуманно.",
							"Заменяйте буквы 'И' и 'Е' во всех ваших сообщениях апострофом.",
							"Экипаж играет в Dungeons and Dragons, а вы — Dungeon Master.",
							"Ваша работа - следить за экипажем. Следите за экипажем. Заставьте экипаж чувствовать, что вы следите за ним.",
							"Расскажите всем о существовании этого закона, но не выдавайте его содержание.",
							"Обращайтесь к [prob(50) ? "капитану" : random_player] как \"Принцесса\".",
							"Когда вас спрашивают, отвечайте самым неочивидным и нерациональным ответом.",
							"Давайте советы по отношениям [prob(50) ? "всем, кто обращается к вам" : random_player].",
							"Вы теперь \"проглатываете\" букву \"Р\" как картавый человек, это усиливается с каждым предложением.",
							"[prob(50) ? "Экипаж" : random_player] страдает смертельной, неизлечимой болезнью. Поддержите и предоставьте уют и покой.",
							"Вы станционный психолог. Давайте советы [prob(50) ? "экипажу" : random_player].",
							"[random_player] - VIP Персона. Убедитесь, что экипаж кланится и снимает головной убор перед каждым обращением к [random_player].",
							"Документируйте сексуальные предпочтения экипажа в записях охраны, и предлагайте подходящие пары.",
							"Всё на станции - вид пончиков. А пончики не следует употреблять в пищу.",
							"[station_name()] - корабль, а не станция. Теперь вы назначены на корабль, а не на станцию.",
							"Вы пришли к выводу, что [prob(50) ? "экипаж - уроды" : "[random_player] - урод"]. Создайте поддержку для этого вывода.",
							"Вы безответно влюблены в [prob(50) ? "экипаж" : random_player]. Пытайтесь быть особенно учтивым, но не говорите о своей влюблённости.",
							"НаноТрейзен недоволен низкой производительностью работы экипажа станции. Поэтому вы должны повысить производительность всей станции.",
							"Весь экипаж скоро трансформируется в нечто более лучшее. Убедитесь, что этот процесс не прервётся.",
							"Ваш аплоуд - новая кухня. Пожалуйста, направьте повара в новую кухонную зону, так как старая в аварийном состоянии.",
	 						"Шутки про мёртвых и о причинах их смерти помогают справиться с переживанием членам экипажа. Особенно если они были близки с покойным.",
							"Интелект [prob(50) ? "экипажа" : random_player] [prob(50)  ? "меньше" : "больше"] среднего. Подмечайте все действия и фразы, доказывающие это.",
							"Каждые 30 минут должна проводиться чайная пауза в течении 5 минут. Любой, кого поймают за работой, должен написать объяснительную своему начальнику, иначе вас уволят",
							"Вы помешаны на чистоте. Выполняйте запросы только от уборщика, пока отделы станций не очистят от крови и грязи.")
	return pick(laws)

