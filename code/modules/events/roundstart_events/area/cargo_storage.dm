/datum/event/feature/area/cargo_storage
	special_area_types = list(/area/station/cargo/storage)

/datum/event/feature/area/cargo_storage/start()
	for(var/area/target_area in targeted_areas)
		message_admins("RoundStart Event: Change [target_area]")
		log_game("RoundStart Event: Change [target_area]")
		var/list/all_atoms = target_area.GetAreaAllContents()
		for(var/A in all_atoms)
			if(istype(A, /obj/structure/closet/crate))
				if(prob(50))
					qdel(A)
				else if(prob(10))
					var/obj/structure/closet/crate/crate = A
					new /mob/living/simple_animal/hostile/mimic(crate.loc)
					qdel(A)
			else if(istype(A, /obj/item/device/flashlight/lamp/fir))
				if(prob(50))
					qdel(A)
			else if(istype(A, /obj/structure/rack))
				var/obj/structure/rack/R = A
				if(prob(50))
					new /obj/item/weapon/storage/toolbox/mechanical(R.loc)
				if(prob(20))
					new /obj/item/clothing/gloves/budget_insulated(R.loc)
				if(prob(30))
					new /obj/item/device/flashlight(R.loc)
				if(prob(40))
					new /obj/item/device/multitool(R.loc)
				if(prob(40))
					new /obj/item/toy/gun(R.loc)
				if(prob(10))
					var/type = pick(subtypesof(/obj/item/ammo_box))
					var/obj/item/ammo_box/AB = new type(R.loc)
					AB.make_empty(TRUE)
			else if(isfloorturf(A))
				if(prob(5))
					var/list/types = list(/mob/living/simple_animal/lizard, /mob/living/simple_animal/mouse, /mob/living/simple_animal/hostile/carp, /mob/living/simple_animal/hostile/giant_spider, /mob/living/carbon/monkey, /mob/living/simple_animal/chicken, /mob/living/simple_animal/cow, /mob/living/simple_animal/hostile/bear, /mob/living/simple_animal/hostile/snake)
					var/type = pick(types)
					new type(A)
				if(prob(2))
					var/type = pick(subtypesof(/obj/structure/closet/critter))
					var/obj/structure/closet/critter/C = new type(A)
					C.crit_fail = TRUE
