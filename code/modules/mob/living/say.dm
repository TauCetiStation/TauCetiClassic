var/global/list/department_radio_keys = list(
	  ":r" = "right ear",	"#r" = "right ear",		".r" = "right ear",
	  ":l" = "left ear",	"#l" = "left ear",		".l" = "left ear",
	  ":i" = "intercom",	"#i" = "intercom",		".i" = "intercom",
	  ":h" = "department",	"#h" = "department",	".h" = "department",
	  ":c" = "Command",		"#c" = "Command",		".c" = "Command",
	  ":n" = "Science",		"#n" = "Science",		".n" = "Science",
	  ":m" = "Medical",		"#m" = "Medical",		".m" = "Medical",
	  ":e" = "Engineering", "#e" = "Engineering",	".e" = "Engineering",
	  ":s" = "Security",	"#s" = "Security",		".s" = "Security",
	  ":w" = "whisper",		"#w" = "whisper",		".w" = "whisper",
	  ":b" = "binary",		"#b" = "binary",		".b" = "binary",
	  ":a" = "alientalk",	"#a" = "alientalk",		".a" = "alientalk",
	  ":t" = "Syndicate",	"#t" = "Syndicate",		".t" = "Syndicate",
	  ":u" = "Supply",		"#u" = "Supply",		".u" = "Supply",
	  ":g" = "changeling",	"#g" = "changeling",	".g" = "changeling",
	  ":d" = "dronechat",	"#d" = "dronechat",		".d" = "dronechat",
	  ":z" = "mafia",		"#z" = "mafia",			".z" = "mafia",

	  ":R" = "right ear",	"#R" = "right ear",		".R" = "right ear",
	  ":L" = "left ear",	"#L" = "left ear",		".L" = "left ear",
	  ":I" = "intercom",	"#I" = "intercom",		".I" = "intercom",
	  ":H" = "department",	"#H" = "department",	".H" = "department",
	  ":C" = "Command",		"#C" = "Command",		".C" = "Command",
	  ":N" = "Science",		"#N" = "Science",		".N" = "Science",
	  ":M" = "Medical",		"#M" = "Medical",		".M" = "Medical",
	  ":E" = "Engineering",	"#E" = "Engineering",	".E" = "Engineering",
	  ":S" = "Security",	"#S" = "Security",		".S" = "Security",
	  ":W" = "whisper",		"#W" = "whisper",		".W" = "whisper",
	  ":B" = "binary",		"#B" = "binary",		".B" = "binary",
	  ":A" = "alientalk",	"#A" = "alientalk",		".A" = "alientalk",
	  ":T" = "Syndicate",	"#T" = "Syndicate",		".T" = "Syndicate",
	  ":U" = "Supply",		"#U" = "Supply",		".U" = "Supply",
	  ":G" = "changeling",	"#G" = "changeling",	".G" = "changeling",
	  ":D" = "dronechat",	"#D" = "dronechat",		".D" = "dronechat",
	  ":Z" = "mafia",		"#Z" = "mafia",			".Z" = "mafia",

	  //kinda localization -- rastaf0
	  //same keys as above, but on russian keyboard layout. This file uses cp1251 as encoding.
	  ":к" = "right ear",	"#к" = "right ear",		".к" = "right ear",
	  ":д" = "left ear",	"#д" = "left ear",		".д" = "left ear",
	  ":ш" = "intercom",	"#ш" = "intercom",		".ш" = "intercom",
	  ":р" = "department",	"#р" = "department",	".р" = "department",
	  ":с" = "Command",		"#с" = "Command",		".с" = "Command",
	  ":т" = "Science",		"#т" = "Science",		".т" = "Science",
	  ":ь" = "Medical",		"#ь" = "Medical",		".ь" = "Medical",
	  ":у" = "Engineering",	"#у" = "Engineering",	".у" = "Engineering",
	  ":ы" = "Security",	"#ы" = "Security",		".ы" = "Security",
	  ":ц" = "whisper",		"#ц" = "whisper",		".ц" = "whisper",
	  ":и" = "binary",		"#и" = "binary",		".и" = "binary",
	  ":ф" = "alientalk",	"#ф" = "alientalk",		".ф" = "alientalk",
	  ":е" = "Syndicate",	"#е" = "Syndicate",		".е" = "Syndicate",
	  ":г" = "Supply",		"#г" = "Supply",		".г" = "Supply",
	  ":п" = "changeling",	"#п" = "changeling",	".п" = "changeling",
	  ":в" = "dronechat",	"#в" = "dronechat",		".в" = "dronechat",
	  ":я" = "mafia",		"#я" = "mafia",			".я" = "mafia",

	  ":К" = "right ear",	"#К" = "right ear",		".К" = "right ear",
	  ":Д" = "left ear",	"#Д" = "left ear",		".Д" = "left ear",
	  ":Ш" = "intercom",	"#Ш" = "intercom",		".Ш" = "intercom",
	  ":Р" = "department",	"#Р" = "department",	".Р" = "department",
	  ":С" = "Command",		"#С" = "Command",		".С" = "Command",
	  ":Т" = "Science",		"#Т" = "Science",		".Т" = "Science",
	  ":Ь" = "Medical",		"#Ь" = "Medical",		".Ь" = "Medical",
	  ":У" = "Engineering",	"#У" = "Engineering",	".У" = "Engineering",
	  ":Ы" = "Security",	"#Ы" = "Security",		".Ы" = "Security",
	  ":Ц" = "whisper",		"#Ц" = "whisper",		".Ц" = "whisper",
	  ":И" = "binary",		"#И" = "binary",		".И" = "binary",
	  ":Ф" = "alientalk",	"#Ф" = "alientalk",		".Ф" = "alientalk",
	  ":Е" = "Syndicate",	"#Е" = "Syndicate",		".Е" = "Syndicate",
	  ":Г" = "Supply",		"#Г" = "Supply",		".Г" = "Supply",
	  ":П" = "changeling",	"#П" = "changeling",	".П" = "changeling",
	  ":В" = "dronechat",	"#В" = "dronechat",		".В" = "dronechat",
	  ":Я" = "mafia",		"#Я" = "mafia",			".Я" = "mafia",
)

/mob/living/proc/binarycheck()
	if (ispAI(src))
		return
	if (issilicon(src))
		return 1
	if (!ishuman(src))
		return
	var/mob/living/carbon/human/H = src
	if (H.l_ear || H.r_ear)
		var/obj/item/device/radio/headset/dongle
		if(istype(H.l_ear,/obj/item/device/radio/headset))
			dongle = H.l_ear
		else
			dongle = H.r_ear
		if(!istype(dongle))
			return
		if(dongle.translate_binary)
			return 1

/mob/living/say(message, datum/language/speaking = null, verb="says", alt_name="", italics=FALSE, message_range = world.view, list/used_radios = list(), sound/speech_sound, sound_vol, sanitize = TRUE, message_mode = FALSE)
	if (src.client)
		if(client.prefs.muted & MUTE_IC)
			to_chat(src, "You cannot send IC messages (muted).")
			return
		if (client.handle_spam_prevention(message,MUTE_IC))
			return
	if(sanitize)
		message = sanitize(message)
		if(!message)
			return

		if(message[1] == "*")
			return emote(copytext(message, 2))

		message = capitalize(trim(message))
		message = add_period(message)

	var/turf/T = get_turf(src)

	//log
	var/area/A = get_area(src)
	log_say("[key_name(src)] : \[[A.name][message_mode?"/[message_mode]":""]\]: [message]")

	//handle nonverbal and sign languages here
	if (speaking)
		if (speaking.flags & NONVERBAL)
			if(prob(30))
				me_emote("[pick(speaking.signlang_verb)].", SHOWMSG_AUDIO)

		if (speaking.flags & SIGNLANG)
			say_signlang(message, pick(speaking.signlang_verb), speaking)
			last_phrase = message
			return 1

	//speaking into radios
	if(used_radios.len)
		italics = 1
		message_range = 1

		if (!isAI(src)) // Atlantis: Prevents nearby people from hearing the AI when it talks using it's integrated radio.
			for(var/mob/living/M in hearers(5, src))
				if(M != src)
					M.show_message("<span class='notice'>[src] talks into [used_radios.len ? used_radios[1] : "the radio."]</span>", SHOWMSG_VISUAL|SHOWMSG_AUDIO)
				if (speech_sound)
					playsound_local(src, speech_sound, VOL_EFFECTS_MASTER, sound_vol * 0.5)

		speech_sound = null	//so we don't play it twice.

	//make sure the air can transmit speech
	var/datum/gas_mixture/environment = T.return_air()
	if(environment)
		var/pressure = environment.return_pressure()
		if(pressure < SOUND_MINIMUM_PRESSURE)
			italics = 1
			message_range = 1

			if (speech_sound)
				sound_vol *= 0.5	//muffle the sound a bit, so it's like we're actually talking through contact

	//make sure we actually can hear there
	if (T.sound_coefficient < 0.5)
		message = Gibberish(message, (1.0 - max(0.0, T.sound_coefficient)) * 100 + 20)

	var/list/listening = list()
	var/list/listening_obj = list()
	var/list/listening_talking_atoms = list()

	if(T) // todo: get_hearers_in_view
		var/list/hear = hear(message_range, T)
		var/list/hearturfs = list()

		for(var/atom/movable/AM in hear)
			listening |= AM.get_listeners()
			listening_obj |= AM.get_listening_objs()
			hearturfs += AM.locs[1]

		// need to do second pass because of mobs who can hold objects who can hold mobs
		// also because of half baked talking_atom
		// future refactoring needed
		for(var/atom/movable/AM in listening_obj)
			if(AM.flags & HEAR_PASS_SAY)
				listening |= AM.get_listeners()
			if(AM.flags & HEAR_TA_SAY)
				listening_talking_atoms += AM
			if(!(AM.flags & HEAR_TALK)) // if we don't need it anymore
				listening_obj -= AM

		for(var/mob/M in player_list)
			if(QDELETED(M)) // avoid not hard-deleted mobs with client
				continue
			if(M.stat == DEAD && M.client && (M.client.prefs.chat_toggles & CHAT_GHOSTEARS))
				listening |= M
				continue
			if(M.loc && (M.locs[1] in hearturfs))
				listening |= M

	//speech bubble
	var/list/speech_bubble_recipients = list()
	for(var/mob/M in listening)
		if(M.client)
			speech_bubble_recipients.Add(M.client)
	var/image/I = image('icons/mob/talk.dmi', src, "[typing_indicator_type][say_test(message)]", MOB_LAYER + 1)
	I.appearance_flags = APPEARANCE_UI_IGNORE_ALPHA|KEEP_APART
	I.mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	INVOKE_ASYNC(GLOBAL_PROC, GLOBAL_PROC_REF(flick_overlay), I, speech_bubble_recipients, 30)
	for(var/mob/M in listening)
		M.hear_say(message, verb, speaking, alt_name, italics, src, used_radios.len, speech_sound, sound_vol)

	for(var/obj/O in listening_obj)
		spawn(0)
			if(O) //It's possible that it could be deleted in the meantime.
				O.hear_talk(src, message, verb, speaking)

	for(var/atom/TA in listening_talking_atoms)
		SEND_SIGNAL(TA, COMSIG_MOVABLE_HEAR, message, src)

	last_phrase = message

	return 1

/mob/living/proc/say_signlang(message, verb="gestures", datum/language/language)
	for (var/mob/O in viewers(src, null))
		O.hear_signlang(message, verb, language, src)

/obj/effect/speech_bubble
	var/mob/parent

/mob/living/init_languages()
	for(var/name in all_languages)
		var/datum/language/L = all_languages[name]
		if(languages[L] >= LANGUAGE_CAN_SPEAK)
			continue

		for(var/sound in L.approximations)
			add_approximation(sound, L.approximations[sound])
